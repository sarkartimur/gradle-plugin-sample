plugins {
    id 'groovy'
    id 'maven-publish'
    id 'java-gradle-plugin'
}

group 'org.example'
version '1.0'

repositories {
    mavenCentral()
    mavenLocal()
    gradlePluginPortal()
}

dependencies {
    // groovy version should be the same as in "./gradlew --version" output
    implementation 'org.codehaus.groovy:groovy-all:3.0.7'

    // plugin dependencies
    implementation(
            'org.springframework.boot:spring-boot-gradle-plugin:2.6.3',
            'io.spring.gradle:dependency-management-plugin:1.0.11.RELEASE',
            'io.freefair.gradle:lombok-plugin:6.4.1',
            'gradle.plugin.com.google.cloud.tools:jib-gradle-plugin:3.2.0'
    )
}

/*
 *  To publish the plugin to local maven repository
 *  execute the 'publishSimplePluginPluginMarkerPublicationToMavenLocal'
 *  followed by the 'publishToMavenLocal' task.
 *  To use this plugin in an external project,
 *  add the 'pluginManagement' block with mavenLocal(), mavenCentral() and gradlePluginPortal()
 *  repositories as the first lines of settings.gradle in the project that uses this plugin
 */
gradlePlugin {
    plugins {
        simplePlugin {
            id = 'org.example.configurer-gradle-plugin'
            implementationClass = 'org.example.ConfigurerPlugin'
        }
    }
}